
allprojects {
    repositories{
        mavenCentral()
        maven{ url "https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository" }
        maven{ url 'https://www.jitpack.io' }
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven{ url "https://oss.sonatype.org/content/repositories/releases/" }
    }

    ext{
        asmVersion = "9.7"
        mindustryVersion = "v146"
        arcVersion = "v146"
        rhinoVersion = "73a812444a"
        jabelVersion = "93fde537c7"

        //windows sucks
        isWindows = System.getProperty("os.name").toLowerCase().contains("windows")
        sdkRoot = System.getenv("ANDROID_HOME") ?: System.getenv("ANDROID_SDK_ROOT")

        androidPlatformRoot = new File("$sdkRoot/platforms/").listFiles().sort().reverse().find{ f -> new File(f, "android.jar").exists()}

        check = {
            if(!sdkRoot || !new File(sdkRoot).exists()) throw new GradleException("No valid Android SDK found. Ensure that ANDROID_HOME is set to your Android SDK directory.");
            if(!androidPlatformRoot) throw new GradleException("No android.jar found. Ensure that you have an Android platform installed.")
        }
    }

    tasks.withType(JavaCompile) {
        java {
            sourceCompatibility = JavaVersion.VERSION_17
            targetCompatibility = 8
        }
        options.compilerArgs += ["-Xlint:deprecation"]
        options.compilerArgs.addAll(['--release', '8'])
    }
}

project(":launcher")
project(":annotation")
project(":core")
project(":tool")
project(":desktop")
project(":android")

task modJar(type: Jar) {
    archiveFileName = "finalCampaign.jar"
    destinationDirectory = layout.buildDirectory.dir("libs")

    dependsOn ":core:jar"
    dependsOn ":desktop:jar"

    into("/") {
        from([zipTree("$rootDir/core/build/libs/assets.jar"), 
              zipTree("$rootDir/core/build/libs/core.jar")])
        {
            exclude "/META-INF/**"
            include "**"
        }
    }

    into("/fcLaunch/") {
        from("$rootDir/desktop/build/libs/") {
            include "desktop.jar"
            rename { "launcher.jar" }
        }
    }
}

task modApk() {
    dependsOn ":tool:buildAndroid"
}

task deploy() {
    dependsOn "modJar"
    dependsOn "modApk"
}